subprojects {
    apply plugin: 'eu.xenit.docker-alfresco'
    apply plugin: 'eu.xenit.docker-compose'
    apply plugin: 'be.vbgn.ci-detect'

    dockerAlfresco {
        // leanImage speeds up build (see https://bitbucket.org/xenit/xenit-gradle-plugins/src/master/README.md)
        leanImage = true
    }

    // Isolate the version number ("docker-231" -> "231"). We should find a cleaner way
    def subproject_alfresco_version = project.name.split("-").last()
    dependencies {
        alfrescoAmp project(path: ":alfresco:${subproject_alfresco_version}", configuration: 'ampArtifact')
        // Read in the alfresco V 23.x in with alfred-api amps
        alfrescoAmp project(path: ":alfred-api-integrationtests-server", configuration: 'amp')
        // Read in the alfresco integration-test-server
        alfrescoAmp project(path: ':alfred-api-integrationtests-client:model-amp', configuration: 'ampArchives')
        // Read the model in for running the integration tests.
        alfrescoSM "com.gradecak.alfresco-mvc:alfresco-mvc-rest:${mvc}"
        alfrescoSM "com.gradecak.alfresco-mvc:alfresco-mvc-aop:${mvc}"
        alfrescoSM files(jar)
    }

    dockerCompose {
        useComposeFiles = ['docker-compose.yml']
        tcpPortsToIgnoreWhenWaiting = [ 8000 ] // Don't get stuck when a debugger is attached
        // Don't use the dev compose file during CI builds
        if (!ci.isCi()) {
            // Allow the dev compose file to be deleted
            def extraComposeFile = 'debug-extension.docker-compose.yml'
            if (project.file(extraComposeFile).exists()) {
                useComposeFiles.add(extraComposeFile)
            }
        }
    }
}