subprojects {
    apply from: "${project.projectDir}/overrides.gradle"
    apply from: "${rootProject.projectDir}/publish.gradle"
    apply plugin: 'eu.xenit.amp'

    configurations {
        ampArtifact
    }

    artifacts {
        ampArtifact amp
    }

    sourceSets {
        main {
            amp {
                module {
                    it.put("module.id", "alfred-api-${project.name}")
                    it.put("module.title", "alfred-api-${project.name}")
                    it.put("module.description",
                            "Xenit Alfred API implementation Alfresco ${alfresco_version.substring(0, 3)}")
                    it.put("module.version", project.version)
                    it.put("module.repo.version.min", project.alfresco_min_version)
                    if(project.hasProperty("alfresco_max_version")
                            && project.alfresco_max_version != null) {
                        it.put("module.repo.version.max", project.alfresco_max_version)
                    }
                }
            }
        }
    }

    dependencies {
        implementation(project(":apix-impl"))
        implementation(project(":apix-impl:apix-impl-${project.name}"))
        implementation(project(":apix-rest-v1"))
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifactId "alfred-api-${project.name}"
                artifact tasks.amp
            }
        }
    }

    project.tasks.jar.enabled = false

    // Extend amp plugin task:
    // We want to add 'alfresco-global.properties' etc to the module-specific folder in the AMP
    // (e.g. 'config/module/alfresco/module/apix-impl-61/') without hard-coding project name for each Alfresco version.
    amp {
        archiveBaseName = "alfred-api-${project.name}"
        into("config/alfresco/module/${project.name}") {
            from("${rootProject.projectDir}/src/config/alfresco-global.properties")
            from("${rootProject.projectDir}/src/config/log4j.properties")
            from("${rootProject.projectDir}/src/config/module-context.xml")
            expand(moduleId: project.name)
        }
        into("config/alfresco/module/${project.name}/messages") {
            from("${rootProject.projectDir}/src/config/messages/")
        }
    }

    afterEvaluate {
        signMavenPublication.dependsOn amp
    }
}
