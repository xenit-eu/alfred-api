subprojects {
    apply plugin: 'eu.xenit.docker-alfresco'
    apply plugin: 'eu.xenit.docker-compose'
    apply plugin: 'be.vbgn.ci-detect'

    dockerAlfresco {
        // leanImage speeds up build (see https://bitbucket.org/xenit/xenit-gradle-plugins/src/master/README.md)
        leanImage = true
    }

    // Isolate the version number ("docker-52" -> "52"). We should find a cleaner way
    def subproject_alfresco_version = project.name.substring(project.name.length() - 2)

    dependencies {
        alfrescoAmp project(path: ":apix-impl:apix-impl-${subproject_alfresco_version}", configuration: 'ampArtifact')
        alfrescoAmp project(path: ':apix-integrationtests:model-amp', configuration: 'ampArchives')
        alfrescoAmp project(path: ':apix-rest-v1', configuration: 'ampArtifact')
        alfrescoSM "com.gradecak.alfresco-mvc:alfresco-mvc-rest:$mvc"
        alfrescoSM "com.gradecak.alfresco-mvc:alfresco-mvc-aop:$mvc"
        alfrescoSM files(jar)
        alfrescoAmp "eu.xenit:alfresco-dynamic-extensions-repo-${subproject_alfresco_version}:${de_version}@amp"
    }

    dockerCompose {
        useComposeFiles = ['docker-compose.yml']
        // Don't use the dev compose file on Jenkins
        if (!ci.isCi()) {
            // Allow the dev compose file to be deleted
            def extraComposeFile = 'debug-extension.docker-compose.yml'
            if (project.file(extraComposeFile).exists()) {
                useComposeFiles.add(extraComposeFile)
            }
        }

        // Solr Tracking improvements
//        environment.put 'GLOBAL_alfresco.cron', '0/2 * * * * ? *'
//        environment.put 'GLOBAL_alfresco.lag', '500'
//        environment.put 'GLOBAL_solr.http.socket.timeout','10000'
        // Logging improvements
        environment.put 'TERM', 'xterm'
        if(subproject_alfresco_version.startsWith("7")) {
            environment.put'GLOBAL_messaging.broker.url','failover:(nio://activemq:61616)?timeout=3000&jms.useCompression=true'
            environment.put'GLOBAL_localTransform.core-aio.url','http://transform-core-aio:8090/'
            // Needed for Solr indexing
            environment.put 'GLOBAL_local.transform.service.enabled','true'
            // Disable transformations and renditions
            environment.put 'GLOBAL_contentPropertyRestrictions.enabled','false'
            environment.put 'GLOBAL_ooo.enabled','false'
            environment.put 'GLOBAL_jodconverter.enabled','false'
            environment.put 'GLOBAL_transform.service.enabled','false'
            environment.put 'GLOBAL_legacy.transform.service.enabled','false'
        }
    }

}
