subprojects {
    apply plugin: 'eu.xenit.docker-alfresco'
    apply plugin: 'eu.xenit.docker-compose'
    apply plugin: 'be.vbgn.ci-detect'
    // To delete when version 7.x no longer is supported
    // Apply the overload.gradle untill no more Alfresco 7.x versions are supported.
    if (project.name.contains("7")){
        apply from: "${project.projectDir}/overrides.gradle"
    }

    dockerAlfresco {
        // leanImage speeds up build (see https://bitbucket.org/xenit/xenit-gradle-plugins/src/master/README.md)
        leanImage = true
    }

    // Isolate the version number ("docker-52" -> "52"). We should find a cleaner way
    def subproject_alfresco_version = project.name.substring(project.name.length() - getTotalNumbersAfterDash(project.name))
    println("subproject_alfresco_version -  ${subproject_alfresco_version}" )
    dependencies {
        alfrescoAmp project(path: ":alfresco:${subproject_alfresco_version}", configuration: 'ampArtifact')
        alfrescoAmp project(path: ':apix-integrationtests:model-amp', configuration: 'ampArchives')
        alfrescoSM "com.gradecak.alfresco-mvc:alfresco-mvc-rest:${mvc}"
        alfrescoSM "com.gradecak.alfresco-mvc:alfresco-mvc-aop:${mvc}"
        alfrescoSM files(jar)

        // only supported untill Alfresco V7.4
        if (subproject_alfresco_version.contains("7")) {
            alfrescoAmp "eu.xenit:alfresco-dynamic-extensions-repo-${subproject_alfresco_version}:${de_version}@amp"
        }
    }

    dockerCompose {
        useComposeFiles = ['docker-compose.yml']
        // Don't use the dev compose file during CI builds
        if (!ci.isCi()) {
            // Allow the dev compose file to be deleted
            def extraComposeFile = 'debug-extension.docker-compose.yml'
            if (project.file(extraComposeFile).exists()) {
                useComposeFiles.add(extraComposeFile)
            }
        }
    }
}

def getTotalNumbersAfterDash(String version) {
    return version.split("-")[1].length()
}