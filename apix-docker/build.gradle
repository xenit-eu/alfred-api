plugins {
    id 'eu.xenit.docker-alfresco' version '5.0.0' apply false
    id 'eu.xenit.docker-compose' version '5.0.0' apply false
    id 'be.vbgn.ci-detect' version '0.1.0' apply false
}

subprojects {
    apply plugin: 'eu.xenit.docker-alfresco'
    apply plugin: 'eu.xenit.docker-compose'
    apply plugin: 'be.vbgn.ci-detect'

    dockerAlfresco {
        // leanImage speeds up build (see https://bitbucket.org/xenit/xenit-gradle-plugins/src/master/README.md)
        leanImage = true
    }

    // Isolate the version number ("docker-52" -> "52"). We should find a cleaner way
    def subproject_alfresco_version = project.name.substring(project.name.length() - 2)

    dependencies {
        alfrescoAmp project(path: ":apix-impl:apix-impl-${subproject_alfresco_version}", configuration: 'ampArtifact')
        alfrescoAmp project(path: ':apix-integrationtests-model-amp', configuration: 'ampArchives')
        alfrescoAmp "eu.xenit:alfresco-dynamic-extensions-repo-${subproject_alfresco_version}:${de_version}@amp"
    }

    dockerCompose {
        useComposeFiles = ['docker-compose.yml']
        // Don't use the dev compose file on Jenkins
        if(!ci.isCi()) {
            // Allow the dev compose file to be deleted
            def extraComposeFile = 'debug-extension.docker-compose.yml'
            if(project.file(extraComposeFile).exists()) {
                useComposeFiles.add(extraComposeFile)
            }
        }
    }

}
