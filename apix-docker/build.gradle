subprojects {
    apply plugin: 'eu.xenit.docker-alfresco'
    apply plugin: 'eu.xenit.docker-compose'
    apply plugin: 'be.vbgn.ci-detect'

    dockerAlfresco {
        // leanImage speeds up build (see https://bitbucket.org/xenit/xenit-gradle-plugins/src/master/README.md)
        leanImage = true
    }

    // Isolate the version number ("docker-52" -> "52"). We should find a cleaner way
    // todo: should now work for v 231 & 232
    def subproject_alfresco_version = project.name.substring(project.name.length() - getTotalNumbersAfterDash(project.name))

    dependencies {
        alfrescoAmp project(path: ":alfresco:${subproject_alfresco_version}", configuration: 'ampArtifact')
        alfrescoAmp project(path: ':apix-integrationtests:model-amp', configuration: 'ampArchives')
        alfrescoSM "com.gradecak.alfresco-mvc:alfresco-mvc-rest:${mvc}" // todo change for 231 232
        alfrescoSM "com.gradecak.alfresco-mvc:alfresco-mvc-aop:${mvc}"// todo change for 231 232
        alfrescoSM files(jar)
        alfrescoAmp "eu.xenit:alfresco-dynamic-extensions-repo-${subproject_alfresco_version}:${de_version}@amp"
    }

    dockerCompose {
        useComposeFiles = ['docker-compose.yml']
        // Don't use the dev compose file during CI builds
        if (!ci.isCi()) {
            // Allow the dev compose file to be deleted
            def extraComposeFile = 'debug-extension.docker-compose.yml'
            if (project.file(extraComposeFile).exists()) {
                useComposeFiles.add(extraComposeFile)
            }
        }
    }
}

def getTotalNumbersAfterDash(String version) {
    return version.split("-")[1].length()
}