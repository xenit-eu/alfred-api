plugins {
    id 'eu.xenit.de' version '2.0.2' apply false
    id 'eu.xenit.amp' version '1.0.0' apply false
    id 'eu.xenit.alfresco' version '1.0.0' apply false
}

String getMaxRepositoryVersionRequirement(Project currentProject, String minAlfrescoVersion) {
    Comparator<Project> comparator = new Comparator<Project>() {
        @Override
        int compare(Project p1, Project p2) {
            return p1.name.compareTo(p2.name)
        }
    }

    List<Project> subProjectList = subprojects.toSorted(comparator)

    if (currentProject.getName().equals(subProjectList.last().getName())) {
        return ""
    }

    return minAlfrescoVersion + ".99"
}

ext {
    alfresco_50_min_version = "5.0"
    alfresco_51_min_version = "5.1"
    alfresco_52_min_version = "5.2"
    alfresco_60_min_version = "6.0"
    alfresco_61_min_version = "6.1"
}

subprojects {
    apply from: "${rootProject.projectDir}/publish.gradle"
    apply from: "${project.projectDir}/overrides.gradle"
    apply plugin: 'eu.xenit.de'
    apply plugin: 'eu.xenit.amp'
    apply plugin: 'eu.xenit.alfresco'

    publishing {
        publications {
            maven(MavenPublication) {
                artifactId "${project.name}-amp"
                artifact tasks.amp
            }
        }
    }

    configurations {
        ampArtifact
    }

    artifacts {
        ampArtifact amp
    }

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java', 'src/main/java-shared']
            }
            amp {
                dynamicExtension()
                module("src/main/config/module.properties")
            }
        }
        test {
            java {
                srcDirs = ['src/test/java', 'src/test/java-shared']
            }
            testResultsDirName = "${buildDir}/test-results/test"
        }
    }

    dependencies {
        compile(project(":apix-interface"))
        compile(project(":apix-rest-v1")) {
            exclude group: 'org.alfresco'
        }
        alfrescoProvided group: 'org.alfresco', name: 'alfresco-repository', version: alfresco_version
        alfrescoProvided group: 'org.alfresco', name: 'alfresco-remote-api', version: alfresco_version

        compile group: 'org.yaml', name: 'snakeyaml', version: '1.15'
        compile group: 'javax.validation', name: 'validation-api', version: '1.1.0.Final'

        testCompile group: 'eu.xenit.de', name: 'annotations', version: de_version
        testCompile group: 'org.alfresco', name: 'alfresco-repository', version: alfresco_version
        testCompile group: 'org.alfresco', name: 'alfresco-data-model', version: alfresco_dm_version
        testCompile group: 'org.alfresco', name: 'alfresco-remote-api', version: alfresco_version

        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.25.1'
        testCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.2'
        testCompile group: 'eu.xenit.testing', name: 'integration-testing', version: '1.1.0'
    }

    task generateVersionFile(type: Task) {
        def versionFile = "${rootDir}/apix-impl/src/main/java/eu/xenit/apix/Version.java"
        outputs.file versionFile
        doFirst {
            file(versionFile).text = """package eu.xenit.apix;
/*
 * WARNING: THIS FILE IS AUTO-GENERATED BY GRADLE DURING BUILD. 
 * ANY CHANGES TO THIS FILE WILL BE LOST AFTER NEW BUILD.
 * SEE task generateVersionFile IN api-x_dynamicextensions/build.gradle
 */
public class Version {
	public static final String Number = "${project.versionWithoutQualifier}";
}
"""
        }
    }
    compileJava.dependsOn generateVersionFile

    test {
        // Always run unit tests
        outputs.upToDateWhen { false }

        // Show test results when running from the CLI
        testLogging { events "PASSED", "FAILED", "SKIPPED" }
    }

    jar {
        // This BundleSymbolicName is also used in
        // apix-impl/src/integration-test/java/eu/xenit/apix/tests/ApixImplBundleFilter.java
        // to find which integration tests need to run, so when changing it, change it there too.
        bnd(
                'Export-Package': 'eu.xenit.apix.*',
                'Include-Resource': includeResource(configurations.compile),
                'Bundle-ClassPath': bundleClassPath(configurations.compile),
                'Bundle-SymbolicName': "${project.group}.${project.name}",
                'Bundle-Description': 'Alfred API: Java API and REST API',
                'Import-Package': 'org.springframework.beans.factory,'
                        + 'org.springframework.cglib.core,'
                        + 'org.springframework.cglib.proxy,'
                        // Unintuitively, the notation 'version="1.0"' means any version >= 1.0
                        + 'org.json;version="1.0",'
                        + '!org.codehaus.jackson.map,'
                        + '!com.google.appengine.api,'
                        + '!com.google.apphosting.api,'
                        + '!org.joda.convert,'
                        + '*'
        )
    }

    // Extend amp plugin task:
    // We want to add 'alfresco-global.properties' etc to the module-specific folder in the AMP
    // (e.g. 'config/module/alfresco/module/apix-impl-51/') without hard-coding project name for each Alfresco version.
    amp {
        deBundles = files(
                jar,
                "${rootDir}/lib/swagger-ui_5x-1.1.0.jar", // Works for 6x too
        )
        into("config/alfresco/module/${project.name}") {
            from("${project.parent.projectDir}/config/alfresco-global.properties")
            from("${project.parent.projectDir}/config/log4j.properties")
            from("${project.parent.projectDir}/config/module-context.xml")
            expand(moduleId: project.name)
        }
        into("config/alfresco/module/${project.name}/messages") {
            from("${project.parent.projectDir}/config/messages/")
        }
    }

    afterEvaluate {
        signMavenPublication.dependsOn amp
    }

    alfrescoDynamicExtensions {
        versions.dynamicExtensions = de_version
        // Configure endpoint for installBundle task
        repository {
            endpoint {
                protocol = project.hasProperty('protocol') ? project.protocol : 'http'
                host = project.hasProperty('host') ? project.host : 'localhost'
                port = project.hasProperty('port') ? project.port : 8080
            }
        }
    }
}
